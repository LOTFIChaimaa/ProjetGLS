grammar fr.enseeiht.Game with org.eclipse.xtext.common.Terminals

generate game "http://www.enseeiht.fr/Game"

/* Jeu d'exploration
 *	caractérisé par :
 *		- nom
 *	contient toutes les informations liées au jeu :
 *		- lieux à explorer
 *		- chemins reliant les différents lieux
 *		- l'explorateur incarné par le joueur
 *		- les niveaux de difficulté
 *		- les connaissances utilisées dans le jeu
 *		- les objets existant dans le jeu
 *		- les personnes existant dans le jeu
 *		- lieu de départ
 *		- lieu d'arrivée
 */
Jeu:
	'jeu' name=ID '{'
		lieux+=Lieu*
		chemins+=Chemin*
		joueur=Explorateur
		difficultes+=Difficulte+
		connaissances+=Connaissance*
		objets+=Objet*
		personnes+=Personne*
		'lieuDepart' lieuDepart=[Lieu|ID]
		'lieuArrivee' lieuArrivee=[Lieu|ID]
	'}'
;

/* Description
 *	caractérisé par :
 *		- texte de description
 * 		- conditions d'activation de cette description
 */
 Description:
 	'description' '{'
 		'texte' texte=STRING
 		(conditions+=Condition)*
 	'}'
 ;

/* Explorateur
 *	caractérisé par :
 *		- nom
 * 		- difficulté choisie
 *		- taille maximale des objets cumulés
 *		- liste des connaissances possédées
 *		- liste des objets possédés
 *		- lieu actuel
 */
Explorateur:
	'explorateur' name=ID '{'
		'difficulte' difficulte=[Difficulte|ID]
		'tailleMax' tailleMax=INT
		('connaissances' (connaissances+=[Connaissance|ID])+)?
		('objets' (objets+=[Objet|ID])+)?
		'lieuActuel' lieuActuel=[Lieu|ID]
	'}'
;

/* Difficulté
 *	les objets et connaissances en début de partie dépendent de la difficulté
 *	caractérisée par :
 *		- nom
 *		- connaissances
 *		- objets
 */
Difficulte:
	'difficulte' name=ID '{'
		('connaissances' (connaissances+=[Connaissance|ID])+)?
		('objets' (objets+=[Objet|ID])+)?
	'}'
;

/* Connaissance
 *	caractérisé par :
 *		- nom
 *		- descriptions (plusieurs car dépendent de conditions)
 */
Connaissance:
	'connaissance' name=ID '{'
		(descriptions+=Description)+
	'}'
;

/* Personne
 *	caractérisé par :
 *		- nom
 * 		- description
 *		- obligatoire (booléen)
 *		- l'intéraction de la personne
 */
Personne:
	'personne' name=ID '{'
		description=Description
		(obligatoire?='obligatoire')?
		interaction=Interaction
	'}'
;
/* Interaction
 *	caractérisé par :
 *		- les connaissances et objets pouvant être donnés à l'explorateur
 *		- les choix possibles
 */
Interaction:
	'interaction' '{'
		(transmission+=Transmission)*
		(choix+=Choix)+
	'}'
;
/* Choix
 * 	Indique les choix possibles lors d'une interaction
 * 	caractérisé par :
 * 		- actions réalisables par le choix
 * 		- estDebut (booleen)
 * 		- estFin (booleen)
 * 		- les conditions propres au choix
 */
Choix:
	'choix' name=ID '{'
		(actions+=Action)*
		(estDebut?='estDebut')?
		(estFin?='estFin')?
		(conditions+=Condition)*
	'}'
;

/* Action
 * 	caractérisé par :
 * 		- choix nécessaires
 * 		- conditions nécessaires
 * 		- transmissions : donnent les objets et connaissances qui peuvent être donnés ou consommés
 */
 Action:
	'action' '{'
		('choixNecessaires' (choixNecessaires+=[Choix|ID])+)?
		(conditions+=Condition)*
		(transmissions+=Transmission)+
	'}'
;

/* Objet
 *	caractérisé par :
 *		- nom
 *		- descriptions (plusieurs car dépendent de conditions)
 *		- la transformation de l'objet
 *		- taille
 */
Objet:
	'objet' name=ID '{'
		(descriptions+=Description)+
	  	(transformations+=Transformation)*
		'taille' taille=INT
	'}'
;

/* Tranformation
 *  Permet de gérer la transformation des objets
 * 	caractérisé par :
 * 		- conditions de la transformation
 * 		- résultats possibles
 */
Transformation:
	'transformation' '{'
		(conditions+=Condition)
		(resultats+=Resultat)+
	'}'
;

/* Resultat
 *	résultat possible d'une transformation d'objet
 *	caractérisé par :
 * 		- objet résultat
 * 		- conditions d'obtention
 */
Resultat:
  'resultat' '{'
    'objet' objet=Objet
    (conditions+=Condition)*
  '}'
;

/* Lieu
 *	un lieu contient des connaissances, des objets et des personnes
 *	caractérisé par :
 *		- nom
 *		- descriptions (plusieurs car dépendent de conditions)
 *		- connaissances/objets/personnes (trouvables)
 * 		- objets déposables
 *		- les chemins que peut emprunter l'explorateur
 *		- les chemins obligatoire par l'explorateur
 */
Lieu:
	'lieu' name=ID '{'
		(descriptions+=Description)+
		(trouvables+=Trouvable)*
		(deposables+=Deposable)*
		('cheminsPossibles' (cheminsPossibles+=[Chemin|ID])+)?
		('cheminsObligatoires' (cheminsObligatoires+=[Chemin|ID])+)?
	'}'
;

/* Chemin
 *	un chemin relie deux lieux
 *	caractérisé par :
 *		- nom
 *		- descriptions (plusieurs car dépendent de conditions)
 *		- lieu1
 *		- lieu2
 *		- ouvert ? (booléen)
 * 		- condition d'ouverture
 *		- visible ? (booléen)
 * 		- condition de visibilité
 *		- obligatoire? (booléen)
 *		- les objets qui peuvent être donnés lors du passage du chemin (transmission)
 *		- les connaissances qui peuvent être données lors du passage du chemin (transmission)
 */
Chemin:
	'chemin' name=ID '{'
		(descriptions+=Description)+
		'lieu1' lieu1=[Lieu|ID]
		'lieu2' lieu2=[Lieu|ID]
		(ouvert?='ouvert')?
		('ouverture' (conditionsOuverture+=Condition)+)?
		(visible?='visible')?
		('visibilite' (conditionsVisibilite+=Condition)+)?
		(transmissions+=Transmission)*
	'}'
;

/* Transmission
 *	Contient les objets et connaissances qui peuvent être transmis ou consommés durant le passage d'un chemin, ainsi que les conditions
 *	caractérisé par :	   
 *		- les objets qui peuvent être transmis/consommés lors du passage du chemin
 *		- les connaissances qui peuvent être transmises lors du passage du chemin
 *		- les conditions de la transmission et consommation d'objets et de connaissances
 * 		
 */
Transmission:
	'transmission' '{'
		('objetsTransmis' (objetsTransmis+=[Objet|ID])+)?
		('objetsConsommes' (objetsConsommes+=[Objet|ID])+)?
		('connaissances' (connaissances+=[Connaissance|ID])+)?
		(conditions+=Condition)*
	'}'
;

/* Condition
 *	Determine les conditions d'une action quelconque
 *	caractérisé par :
 *		- objets nécessaires
 *		- connaissances nécessaires
 *		- la difficulté
 *		- hasard
 */
Condition:
	'condition' '{'
		('objetsPossedes' (objetsPossedes+=[Objet|ID])+)?
		('objetsManquants' (objetsManquants+=[Objet|ID])+)?
		('connaissancesPossedees' (connaissancesPossedees+=[Connaissance|ID])+)?
		('connaissancesManquantes' (connaissancesManquantes+=[Connaissance|ID])+)?
		'difficulte' difficulte=[Difficulte|ID]
		'hasard' hasard=INT
	'}'
;

/* Trouvable
 *	une connaissance, un objet ou une personne présent(e) dans un lieu peut être dans des états différents :
 *		- visible ? (booléen)
 * 		- conditions de visibilité
 *		- actif ? (booléen)
 * 		- conditions d'activation
 */
Trouvable:
 	('connaissance' entite=[Connaissance|ID] | 'objet' entite=[Objet|ID] | 'personne' entite=[Personne|ID]) '{'
 		(visible?='visible')?
 		('visibilite' (conditionsVisibilite+=Condition)+)?
 		(actif?='actif')?
 		('activation' (conditionsActivation+=Condition)+)?
 	'}'
;

/* Deposable
 *  désigne un objet pouvant être dépose dans un lieu
 * caractérisé par :
 * 		- l'entité concernée
 * 		- conditions nécessaires
 */
Deposable:
	'deposable' entite=[Objet|ID] '{'
		(conditions+=Condition)
	'}'
;
